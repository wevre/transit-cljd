(ns wevre.transit-cljd
  "An implementation of the transit-format for ClojureDart build on top of the
   transit_dart Dart package."
  (:require [cljd.core :as cljd]
            ["package:transit_dart/transit_dart.dart" :as td]))

;; Writing
#_{:clj-kondo/ignore [:unused-binding]}
(def cljd-write-handlers
  (hash-map
    (#/(td/Class cljd/Keyword))
    (reify :extends #/(td/WriteHandler cljd/Keyword dynamic)
      (tag [_ l] ":")
      (rep [_ kw .tag] (.substring (str kw) 1))
      (stringRep [_ kw] (.substring (str kw) 1)))
    (#/(td/Class cljd/Symbol))
    (reify :extends #/(td/WriteHandler cljd/Symbol dynamic)
      (tag [_ _o] "$")
      (rep [_ o .tag] (str o))
      (stringRep [_ o] (str o)))))

;; Reading
(def cljd-read-handlers
  {":" (reify td/ReadHandler (fromRep [_ o] (keyword o)))
   "$" (reify td/ReadHandler (fromRep [_ o] (symbol o)))
   "set"
   (reify td/ArrayReadHandler
     (fromRep [_ _o])
     (arrayBuilder [_]
       (reify :extends td/ArrayBuilder
         (init [_] (transient #{}))
         (add [_ s i] (conj! s i))
         (complete [_ s] (persistent! s)))))
   "list"
   (reify td/ArrayReadHandler
     (fromRep [_ o] o)
     (arrayBuilder [_]
       (reify :extends td/ArrayBuilder
         (init [_] (transient []))
         (add [_ l item] (conj! l item))
         (complete [_ l] (persistent! l)))))
   "cmap"
   (reify td/ArrayReadHandler
     (fromRep [_ _o])
     (arrayBuilder [_]
       (let [marker ^:unique (Object)
             next-key #dart ^:fixed [marker]]
         (reify
           :extends td/ArrayBuilder
           (init [_] (transient {}))
           (add [_ m i]
             (let [k (aget next-key 0)]
               (if (identical? k marker)
                 (do
                   (aset next-key 0 i)
                   m)
                 (do
                   (aset next-key 0 marker)
                   (assoc! m k i)))))
           (complete [_ m] (persistent! m))))))})

;; Builders

(def map-builder
  "docstring"
  (reify
    :extends #/(td/MapBuilder cljd/TransientHashMap cljd/PersistentHashMap dynamic dynamic)
    (init [_] (transient {}))
    (add [_ m k v] (assoc! m k v))
    (complete [_ m] (persistent! m))))

(def array-builder
  "docstring"
  (reify
    :extends #/(td/ArrayBuilder cljd/TransientVector cljd/PersistentVector dynamic)
    (init [_] (transient []))
    (add [_ v i] (conj! v i))
    (complete [_ v] (persistent! v))))

;; Codecs

(defn json
  "docstring"
  ^td/TransitJsonCodec
  [& {:keys [custom-read-handlers
             custom-write-handlers
             map-builder
             array-builder]
      :or {map-builder map-builder
           array-builder array-builder}}]
  (td/TransitJsonCodec
   .customReadHandlers (merge cljd-read-handlers custom-read-handlers)
   .customWriteHandlers (merge cljd-write-handlers custom-write-handlers)
   .mapBuilder map-builder
   .arrayBuilder array-builder))

(defn json-verbose
  "docstring"
  ^td/TransitJsonVerboseCodec
  [& {:keys [custom-read-handlers
             custom-write-handlers
             map-builder
             array-builder]
      :or {map-builder map-builder
           array-builder array-builder}}]
  (td/TransitJsonVerboseCodec
   .customReadHandlers (merge cljd-read-handlers custom-read-handlers)
   .customWriteHandlers (merge cljd-write-handlers custom-write-handlers)
   .mapBuilder map-builder
   .arrayBuilder array-builder))

(defn msgpack
  "docstring"
  ^td/TransitMessagePackCodec
  [& {:keys [custom-read-handlers
             custom-write-handlers
             map-builder
             array-builder]
      :or {map-builder map-builder
           array-builder array-builder}}]
  (td/TransitMessagePackCodec
   .customReadHandlers (merge cljd-read-handlers custom-read-handlers)
   .customWriteHandlers (merge cljd-write-handlers custom-write-handlers)
   .mapBuilder map-builder
   .arrayBuilder array-builder))
